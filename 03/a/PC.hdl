// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
        Inc16 (in=fb, out=w1);
        Mux16 (a=fb, b=w1, sel=inc, out=w2);
        Mux16 (a=w2, b=in, sel=load, out=w3);
        Mux16 (a=w3, b=false, sel=reset, out=w4);
        Register (in=w4, load=true, out=out, out=fb);
}
